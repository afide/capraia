/*
 * Build script configuration
 */
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath(
                'com.google.protobuf:protobuf-gradle-plugin:0.8.4@jar',
                'com.google.gradle:osdetector-gradle-plugin:1.4.0@jar',
                'com.google.guava:guava:18.0@jar',
                'commons-lang:commons-lang:2.6@jar',
                'kr.motd.maven:os-maven-plugin:1.5.0.Final@jar',
        )
    }
}
/*
 * Plugins configurations
 */
plugins {
    id 'project-report'
    id 'org.sonarqube' version '2.6.2'
}
/*
 * Sonar configuration
 */
sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarqube.com'
        property 'sonar.organization', 'afide'
        property 'sonar.login', 'b44b8ee515071e0be85dff14b536a12f112a461f'
    }
}
/*
 * Root-project only configuration
 */
description = 'A sample multi-project layout for Tendermint applications.'
apply from: 'settings/dependencies.gradle'
apply from: 'settings/groups.gradle'
apply from: 'settings/ide-idea.gradle'
/*
 * Common configuration for all projects
 */
allprojects {
    defaultTasks 'assemble'
    configurations.all {
        resolutionStrategy {
            // Cache dynamic versions for $project.capraia.ttl seconds
            cacheDynamicVersionsFor project.capraia.ttl, 'seconds'
            cacheChangingModulesFor project.capraia.ttl, 'seconds'
        }
    }
    // We care mostly about the dependency report for projects that actually publish something to the repository
    project.plugins.withType( BasePlugin.class) {
        project.apply plugin: 'project-report'
    }
    repositories {
        mavenCentral()
    }
}
